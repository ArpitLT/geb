import geb.gradle.browserstack.BrowserStackAccount
import geb.gradle.saucelabs.SauceAccount

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'io.ratpack:ratpack-gradle:1.6.0'
    }
}

apply plugin: "geb-browserstack"
apply plugin: "geb-saucelabs"
apply plugin: ratpack.gradle.RatpackBasePlugin

dependencies {
    compile seleniumDependency, seleniumSupportDependency

    compile project(":module:geb-ast")
    compile project(":module:geb-waiting")
    compile 'org.jodd:jodd-lagarto:3.7.1'
    compile "org.threeten:threeten-extra:$threeTenExtraVersion"

    testCompile "cglib:cglib:2.2", jsoupDependency, ratpack.groovyTest

    sauceConnect sauceConnectDependency
}

test {
    def availableProcessors = isCi ? 2 : Runtime.runtime.availableProcessors().intdiv(2)
    maxParallelForks availableProcessors
}

task allCrossBrowserTests {
    dependsOn 'allSauceLabsTests', 'allBrowserStackTests', 'allLocalCrossBrowserTests'
}

task firefoxLinuxTest(type: Test) {
    maxHeapSize = "512m"
    systemProperty "geb.dockerized.driver", "firefox"
}

allLocalCrossBrowserTests.dependsOn firefoxLinuxTest

sauceLabs {
    browsers {
        safari_mac_8 {
            capabilities platform: "OS X 10.10"
        }
    }
    task {
        maxHeapSize = "512m"
        testClassesDirs = test.testClassesDirs
        classpath = test.classpath
        maxParallelForks 5
    }
    account {
        username = System.getenv(SauceAccount.USER_ENV_VAR)
        accessKey = System.getenv(SauceAccount.ACCESS_KEY_ENV_VAR)
    }
    connect {
        timeout = 10 // minutes
        identifier = UUID.randomUUID().toString()
    }
}

browserStack {
    def applicationAddresses = [8000, 8080, 9000, 9090, 9999].collect { "http://localhost:$it" }
    application(*applicationAddresses)

    browsers {
        android {
            capabilities device: "Samsung Galaxy S6", realMobile: "true"
        }
        chrome_mac_62
        chrome_windows_62
        firefox_windows_47 //newer versions do not currently support mouse interaction commands
        firefox_mac_47 //newer versions do not currently support mouse interaction commands
        create("internet explorer_windows_10")
        create("internet explorer_windows_11")
        create("edge_windows_17")

    }
    task {
        maxHeapSize = "512m"
        testClassesDirs = test.testClassesDirs
        classpath = test.classpath
        maxParallelForks 5
    }
    account {
        username = System.getenv(BrowserStackAccount.USER_ENV_VAR)
        accessKey = System.getenv(BrowserStackAccount.ACCESS_KEY_ENV_VAR)
    }
    local {
        identifier = UUID.randomUUID().toString()
    }
}

task nonIeBrowserStackTests {
    dependsOn 'androidTest', 'chromeMac62Test', 'chromeWindows62Test', 'firefoxWindows47Test', 'firefoxMac47Test'
}

task ieBrowserStackTests {
    dependsOn 'internetExplorerWindows10Test', 'internetExplorerWindows11Test', 'edgeWindows17Test'
}

modifyPom { pom ->
    pom.project {
        name "Geb Core"
        description "Geb (pronounced \"jeb\") is a browser automation solution. It brings together the power of WebDriver, the elegance of jQuery content selection, the robustness of Page Object modelling and the expressiveness of the Groovy language."
    }

    pom.dependencies.findAll { it.groupId == "org.seleniumhq.selenium" }*.scope = "provided"
}